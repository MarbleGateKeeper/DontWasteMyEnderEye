plugins {
    id 'com.github.johnrengelman.shadow'
    id "me.modmuss50.mod-publish-plugin"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    //accessWidenerPath = project(":common").loom.accessWidenerPath

    neoForge {
        //convertAccessWideners = true
        //extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}


publishMods {
    file = tasks.remapJar.archiveFile
    changelog = rootProject.file("changelog/${mod_version}.md").text
    version = mod_version
    type = STABLE
    displayName = "[NeoForge] Homing Ender Eye ${mod_version} for ${minecraft_version}"
    modLoaders.add("neoforge")

    curseforge {
        projectId = "544990"
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        minecraftVersions.add(minecraft_version)
    }

    modrinth {
        projectId = "k7kuEnwe"
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        minecraftVersions.add(minecraft_version)
    }
}

